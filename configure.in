# Process this file with autoconf to produce a configure script.
AC_PREREQ(2.52)

AC_INIT(ximian-connector, 2.2.2, http://bugzilla.ximian.com/enter_bug.cgi?product=Connector)
AC_CONFIG_SRCDIR(storage)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)



AM_CONFIG_HEADER(config.h)

AC_ARG_WITH(e2k-debug,     [  --with-e2k-debug              Allow debugging])
case $withval in
no)
	;;
*)
	AC_DEFINE(E2K_DEBUG, 1, [Define if you want E2K_DEBUG to be available])
	;;
esac

EVOLUTION_API_VERSION=2.2

AC_MSG_CHECKING(Evolution version)
EVOLUTION_VERSION=`pkg-config --modversion evolution-shell-$EVOLUTION_API_VERSION 2>/dev/null`
if test -z "$EVOLUTION_VERSION"; then
	AC_MSG_ERROR(Evolution development libraries not installed)
fi
AC_SUBST(EVOLUTION_VERSION)
AC_MSG_RESULT($EVOLUTION_VERSION)

EVOLUTION_BASE_VERSION=$EVOLUTION_API_VERSION
AC_DEFINE_UNQUOTED(EVOLUTION_BASE_VERSION,"$EVOLUTION_BASE_VERSION",Evolution version)

BASE_VERSION=$EVOLUTION_API_VERSION
AC_DEFINE_UNQUOTED(BASE_VERSION, "$BASE_VERSION", Connector base version)
AC_SUBST(BASE_VERSION)

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl Initialize libtool
AM_PROG_LIBTOOL

dnl ****
dnl i18n
dnl ****
ALL_LINGUAS="ar az bg bn cs de el en_CA en_GB es fi fr gl gu hi hu id it ja ko lt ms nb nl nn no pa pl pt pt_BR rw sq sr sr@Latn sv ta tr uk xh zh_CN zh_TW"

AC_PROG_INTLTOOL
AM_GLIB_GNU_GETTEXT

GETTEXT_PACKAGE=ximian-connector-${BASE_VERSION}
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Package name for gettext])

localedir='$(prefix)/$(DATADIRNAME)/locale'
AC_SUBST(localedir)

dnl *************************
dnl CFLAGS and LIBS and stuff
dnl *************************

GNOME_COMPILE_WARNINGS(maximum)
CFLAGS="$CFLAGS $WARN_CFLAGS"
case $CFLAGS in
*-Wall*)
	CFLAGS="$CFLAGS -Wno-sign-compare"
	;;
esac
dnl AC_DEFINE(G_DISABLE_DEPRECATED,1,[No deprecated glib functions])
dnl AC_DEFINE(GDK_DISABLE_DEPRECATED,1,[No deprecated gdk functions])
dnl AC_DEFINE(GTK_DISABLE_DEPRECATED,1,[No deprecated gtk functions])
dnl AC_DEFINE(GNOME_DISABLE_DEPRECATED,1,[No deprecated gnome functions])
dnl AC_DEFINE(BONOBO_DISABLE_DEPRECATED,1,[No deprecated bonobo functions])
dnl AC_DEFINE(BONOBO_UI_DISABLE_DEPRECATED,1,[No deprecated bonoboui functions])
dnl AC_DEFINE(GCONF_DISABLE_DEPRECATED,1,[No deprecated gconf functions])
dnl AC_DEFINE(LIBGLADE_DISABLE_DEPRECATED,1,[No deprecated libglade functions])

AC_EGREP_HEADER(socklen_t, sys/socket.h, :, AC_DEFINE(socklen_t, int, [Define to "int" if socklen_t is not defined]))

AM_PATH_GLIB_2_0
AM_PATH_ORBIT2
AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
AM_GCONF_SOURCE_2

CONNECTOR_DATADIR='$(datadir)/ximian-connector/$(BASE_VERSION)'
AC_SUBST(CONNECTOR_DATADIR)

EVOLUTION_idldir="`pkg-config --variable=idldir evolution-shell-2.2`"
AC_SUBST(EVOLUTION_idldir)

EVOLUTION_IDL_INCLUDES="`pkg-config --variable=IDL_INCLUDES evolution-shell-2.2`"
AC_SUBST(EVOLUTION_IDL_INCLUDES)

EVOLUTION_privlibdir="`pkg-config --variable=privlibdir evolution-shell-2.2`"
AC_SUBST(EVOLUTION_privlibdir)

EVOLUTION_privlibexecdir="`pkg-config --variable=privlibexecdir evolution-shell-2.2`"
AC_SUBST(EVOLUTION_privlibexecdir)

EVOLUTION_privdatadir="`pkg-config --variable=privdatadir evolution-shell-2.2`"
AC_SUBST(EVOLUTION_privdatadir)

EVOLUTION_imagesdir="`pkg-config --variable=imagesdir evolution-shell-2.2`"
AC_SUBST(EVOLUTION_imagesdir)

CAMEL_providerdir="`pkg-config --variable=camel_providerdir camel-provider-1.2`"
AC_SUBST(CAMEL_providerdir)


PKG_CHECK_MODULES(ADDRESSBOOK, libedata-book-1.2 camel-provider-1.2)
AC_SUBST(ADDRESSBOOK_CFLAGS)

PKG_CHECK_MODULES(CALENDAR, libedata-cal-1.2)
AC_SUBST(CALENDAR_CFLAGS)
AC_SUBST(CALENDAR_LIBS)

PKG_CHECK_MODULES(CAMEL, camel-provider-1.2)
AC_SUBST(CAMEL_CFLAGS)

dnl Should we be using this now ? We could just re-write the code that we use 
dnl from GAL. We are anyway planning to move away from GAL
dnl PKG_CHECK_MODULES(GAL, gal-2.4)
dnl AC_SUBST(GAL_CFLAGS)

PKG_CHECK_MODULES(MAIL, libecal-1.2)
AC_SUBST(MAIL_CFLAGS)

PKG_CHECK_MODULES(EXCHANGE_STORAGE, evolution-shell-2.2 libedataserverui-1.2 libedata-book-1.2 libedata-cal-1.2 libsoup-2.2 libglade-2.0 libgnomeui-2.0 camel-provider-1.2)
AC_SUBST(EXCHANGE_STORAGE_CFLAGS)
AC_SUBST(EXCHANGE_STORAGE_LIBS)

PKG_CHECK_MODULES(LIBEXCHANGE, libsoup-2.2 evolution-shell-2.2 libedataserverui-1.2 libbonobo-2.0 libxml-2.0 gconf-2.0)
AC_SUBST(LIBEXCHANGE_CFLAGS)
AC_SUBST(LIBEXCHANGE_LIBS)

dnl *********************
dnl Pilot license support
dnl *********************
# This does not take leap years into account, but that's not
# important: having the build time be slightly too early is good
# to support slight clock skew anyway.
# 946684800 is 2000-01-01T00:00:00Z.
# Don't change this without testing under /bin/sh on Solaris.
abt=`eval expr \`date '+\( \( %y \* 365 \) + %j - 1 \) \* 24 \* 60 \* 60 + 946684800'\``
case $abt in
"")
	echo "Warning: build time check failed. (Are you on OS X?)."
	echo "Pilot licenses won't work."
	abt="0"
	;;
esac
AC_DEFINE_UNQUOTED(E2K_APPROX_BUILD_TIME, $abt, [Used to prevent clock-setting attacks against pilot licenses])


dnl *********************
dnl OpenLDAP NTLM support
dnl *********************
EVO_LDAP_CHECK(yes)
case $with_openldap in
no)
	AC_ERROR(LDAP support is required for Connector)
	;;
esac

SAVE_CFLAGS="$CFLAGS"
SAVE_LIBS="$LIBS"
CFLAGS="$CFLAGS $LDAP_CFLAGS"
LIBS="$LIBS $LDAP_LIBS"
AC_CHECK_FUNCS(ldap_ntlm_bind)
CFLAGS="$SAVE_CFLAGS"
LIBS="$SAVE_LIBS"

dnl ********
dnl Kerberos
dnl ********
AC_ARG_WITH(krb5, [  --with-krb5=DIR      Location of Kerberos 5 installation], with_krb5="$withval", with_krb5="/usr")
AC_ARG_WITH(krb5-libs, [  --with-krb5-libs=DIR Location of Kerberos 5 libraries], with_krb5_libs="$withval", with_krb5_libs="$with_krb5/lib")
AC_ARG_WITH(krb5-includes, [  --with-krb5-includes=DIR Location of Kerberos 5 headers], with_krb5_includes="$withval", with_krb5_includes="")


mitlibs="-lkrb5 -lk5crypto -lcom_err -lgssapi_krb5"
heimlibs="-lkrb5 -lcrypto -lasn1 -lcom_err -lroken -lgssapi"
AC_CACHE_CHECK([for Kerberos 5], ac_cv_lib_kerberos5,
[
	LDFLAGS_save="$LDFLAGS"
	LDFLAGS="$LDFLAGS -L$with_krb5_libs $mitlibs"
	AC_TRY_LINK_FUNC(krb5_init_context, ac_cv_lib_kerberos5="$mitlibs",
	[
		LDFLAGS="$LDFLAGS_save -L$with_krb5_libs $heimlibs"
		AC_TRY_LINK_FUNC(krb5_init_context, ac_cv_lib_kerberos5="$heimlibs", ac_cv_lib_kerberos5="no")
	])
	LDFLAGS="$LDFLAGS_save"
])
if test "$ac_cv_lib_kerberos5" = "no"; then
	AC_MSG_ERROR(Could not find krb5. Install krb5 or heimdal packages.)
fi

if test "$ac_cv_lib_kerberos5" = "$mitlibs"; then
	AC_DEFINE(HAVE_MIT_KRB5,1,[Define if you have MIT Krb5])
	if test -z "$with_krb5_includes"; then
		KRB5_CFLAGS="-I$with_krb5/include"
	else
		KRB5_CFLAGS="-I$with_krb5_includes"
	fi
  else
	AC_DEFINE(HAVE_HEIMDAL_KRB5,1,[Define if you have Heimdal])
	if test -z "$with_krb5_includes"; then
		KRB5_CFLAGS="-I$with_krb5/include/heimdal"
	else
		KRB5_CFLAGS="-I$with_krb5_includes"
	fi
  fi
	
KRB5_LIBS="-L$with_krb5_libs $ac_cv_lib_kerberos5"
AC_SUBST(KRB5_CFLAGS)
AC_SUBST(KRB5_LIBS)


dnl *******
dnl gtk-doc
dnl *******
dnl Putting a space before the macro call here makes gnome-autogen not
dnl notice it, which makes it not run gtkdocize and copy gtk-doc.make
dnl into the top level where I don't want it.
 GTK_DOC_CHECK([1.0])


dnl ******************************
dnl Makefiles
dnl ******************************

AC_OUTPUT([
Makefile
xntlm/Makefile
lib/Makefile
camel/Makefile
mail/Makefile
addressbook/Makefile
calendar/Makefile
shell/Makefile
storage/Makefile
docs/Makefile
docs/ietf/Makefile
docs/reference/Makefile
po/Makefile.in
])

case $ac_cv_func_ldap_ntlm_bind in
no)
	echo ""
	AC_MSG_WARN([
No NTLM support in OpenLDAP; Plaintext password authentication will be
used when connecting to the Global Catalog server. Consider installing
the evo-openldap package, or building OpenLDAP with the patch in
docs/openldap-ntlm.diff
])
	;;
esac
		

